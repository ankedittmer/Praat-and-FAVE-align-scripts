## chopper
##
## This script takes sound files and corresponding text grids with several labelled sections
## and extracts every labelled segment into its own sound file
##
## Eric Doty - 2011
## University of Pennsylvania - Linguistics Department
##
## Modified by Anke Dittmer 2017



## Set up
path_in$ = "data/" ; change this to your data location!
tier_num = 1	; In what tear number is the transcription that should be used for chopping?
path_out$ = "data/" ; change this to yuor data location!
output_textgrids = 0	; do you want textgrid outputs?
output_labfiles = 0		; do you want lab file outputs?
output_favealign = 1	; do you want to use this for FAVE-align?



## Iterate through all files in the data location
# find all TextGrid files in the data location and create a list
 	Create Strings as file list... filelist 'path_in$'*.TextGrid

# select each file from the textgrid string to work on one at a time
	number_of_files = Get number of strings

# start the for-loop to iterate through all textgrid files
 	for t from 1 to number_of_files
		select Strings filelist
		current_file$ = Get string... t

# open the current text grid and wav files
		Read from file... 'path_in$''current_file$'
		object_name$ = selected$ ("TextGrid")
		Read from file... 'path_in$''object_name$'.wav

# for counting the number of extracted intervals to name our outcoming files	
		int = 0



## Iterate through all intervals in the selected textgrid
# find the relevant intervals
		select TextGrid 'object_name$'
		number_of_intervals = Get number of intervals... tier_num

		for y from 1 to number_of_intervals
# find the label of the interval
			select TextGrid 'object_name$'
			interval_label$ = Get label of interval... tier_num y

# if the interval is labeled (not null)
			if interval_label$ <>"" and interval_label$ <>" " and interval_label$ <>"  " and interval_label$ <> "xxx"

# count the label to name our file
				int = int+1


## Find the starting and end point of the interval and extract this time-section from the sound file
# find the start and end
				begin = Get starting point... tier_num y
				end = Get end point... tier_num y

# open the sound file and extract the utterance
				select Sound 'object_name$'

				Extract part... begin end Rectangular 1.0 no
# save it
				Write to WAV file... 'path_out$''object_name$'_'int'.wav
				Remove



## Create a new TextGrid for the new soundfile if this was wanted (see Set up)			
# make the new TextGrid			
				if output_textgrids == 1
					select TextGrid 'object_name$'
					
					Extract part... begin end Rectangular 1.0 no
					
					Write to text file... 'path_out$''object_name$'_'int'.TextGrid
					Remove
				endif



## Create a new Textfile in Fave-align standard if this was wanted (see Set up)
# make txt in Fave-align standard
				if output_favealign == 1
					new_end = 'end' - 'begin' ; In Fave-align time is relativ to the begining
					fileappend "'path_out$''object_name$'_'int'.txt" 'object_name$''tab$''object_name$''tab$'0'tab$''new_end''tab$''interval_label$''newline$'
				endif



## Create a new lab file (used for Prosodylab-aligner) if this was wanted (see Set up)
# make the lab file
				if output_labfiles == 1
					fileappend "'path_out$''object_name$'_'int'.lab" 'interval_label$'
				endif


			endif
		endfor
		


## Clean up
		select Sound 'object_name$'
		plus TextGrid 'object_name$'
		Remove

	endfor
select Strings filelist
Remove
