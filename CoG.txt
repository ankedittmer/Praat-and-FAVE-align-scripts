## This script takes short wav and corresponding aligned textgrid files and gets the centre of gravity for
## all s and sh sounds. Additional informations in the output file are the participant number, 
## the item (str, sp, shp, etc.) the word in which the item occured, or if the item occured across bounaries two words,
## the style-context in which the words was spoken (naming, storytelling, etc.), the phonemsurrounding from 2 phonemes before
## to two phonemes after the s /sh occurance, the start and duration of the s /sh sound and the centre of gravity (AFTER A PREEMPHASIS!!)
##
## The Centre of gravity is computet after a preemphasis, so it is not comparable to other studies!
##
## Written by Anke Dittmer 2017


# Set Up
path_in$ = "data/" ; change this to your data location!
tier_num = 1 ; textgrid tier number of the phoneme transcriptions
word_tier_num = 2 ; textgrid tier number of the word transcriptions
path_out$ = "data/" ; change this to your data location!


# Preparation of the result file, write the headings of the columns in a file called cog_outcome.txt
fileappend "cog_outcome.txt" 'Filename', 'Participant', 'Item', 'WordContext', 'Across boundary word', 'Style', 
			...'Phonemesurrounding -2', 'Phonemesurrounding -1', 'Phoneme', 'Phonemesurrounding +1', 
			...'Phonemesurrounding +2', 'Start', 'Duration', 'CoG',  'newline$'



# make a list of all text grids in the data directory
 Create Strings as file list... filelist 'path_in$'*.TextGrid

# select each file from the textgrid string to work on one at a time
number_of_files = Get number of strings
 for t from 1 to number_of_files
	select Strings filelist
	current_file$ = Get string... t

# open the text grid and corresponding wav files
	Read from file... 'path_in$''current_file$'
	object_name$ = selected$ ("TextGrid")
	Read from file... 'path_in$''object_name$'.wav

# for counting the number of extracted intervals		
	int = 0

# find the relevant phoneme intervals
	select TextGrid 'object_name$'
	number_of_intervals = Get number of intervals... tier_num

	# go through all intervals and get the labels of the interval and the intervals from two phonemes before
	# the current one to two phonemes after the current one,
	for i from 1 to number_of_intervals
		select TextGrid 'object_name$'
		labeliplusone$ = "0"
		labeliplustwo$ = "0"
		labeliminusone$ = "0"
		labeliminustwo$ = "0"
		labeli$ = Get label of interval... tier_num i
		if (number_of_intervals - i) > 0 ; be sure the next interval is in this textgrid and the textgrid has not ended
			labeliplusone$ = Get label of interval... tier_num i+1
		endif
		if (number_of_intervals - (i+1)) > 0
			labeliplustwo$ = Get label of interval... tier_num i+2
		endif
		if i > 2
			labeliminusone$ = Get label of interval... tier_num i-1
		endif
		if i > 3
			labeliminustwo$ = Get label of interval... tier_num i-2
		endif

		# if the label of the current interval is a s or sh sound, change the item accordingly
		item$ = ""
		if labeli$ == "S" or labeli$ == "SH"
			if labeli$ == "S"
				item$ = "s" 
			endif
			if item$ == "s" and labeliplusone$ == "T"
				item$ = "st"
			endif
			if item$ == "s" and labeliplusone$ == "K"
				item$ = "sk"
			endif
			if item$ == "st" and labeliplustwo$ == "R"
				item$ = "str"
			endif
			if item$ == "sk" and labeliplustwo$ == "R"
				item$ = "skr"
			endif
			if item$ == "s" and labeliplusone$ == "P"
				item$ = "sp"
			endif
			if item$ == "sp" and labeliplustwo$ == "R"
				item$ = "spr"
			endif
			if labeli$ == "SH"
				item$ = "sh"
			endif
			if item$ == "sh" and labeliplusone$ == "T"
				item$ = "sht"
			endif
			if item$ == "sh" and labeliplusone$ == "K"
				item$ = "shk"
			endif
			if item$ == "sht" and labeliplustwo$ == "R"
				item$ = "shtr"
			endif
			if item$ == "shk" and labeliplustwo$ == "R"
				item$ = "shkr"
			endif
			if item$ == "sh" and labeliplusone$ == "P"
				item$ == "shp"
			endif
			if item$ == "shp" and labeliplustwo$ == "R"
				item$ = "shpr"
			endif

			# get the begin, end and duration of the interval (if the phoneme is an s or sh) and round it to 3 decimal digits
			begin = Get starting point... tier_num i
			end = Get end point... tier_num i
			dur = end-begin
			fixed_begin$ = fixed$(begin,3)
			fixed_dur$ = fixed$(dur,3)

			# get the Participant number and the speaking style out of the file-name
			participant = extractNumber(object_name$, "Participant")
			digit_length = length(string$(participant))
			startstyle = 12 + digit_length
			endstyle = length(object_name$)-startstyle-(length(object_name$)-index(object_name$, "_"))
			style$ = mid$(object_name$, startstyle, endstyle)

			# get the word where the item occures, or words if the item occured accross word-boundaries
			if length(item$)>1
				end_phoneme_plusone = Get end point... tier_num i+1
			endif
			if length(item$)>2
				end_phoneme_plustwo = Get end point... tier_num i+2
			endif
			word$ = "NaN"
			across_boundary$ = ""
			number_of_intervals_words = Get number of intervals... word_tier_num
			for j from 1 to number_of_intervals_words ; go through all word intervals and see if the current phoneme interval lies in it
				min = Get starting point... word_tier_num j
				max = Get end point... word_tier_num j
				if min <= begin and max >= end
					word$ = Get label of interval... word_tier_num j
					if j+1 <= number_of_intervals_words
						min_after = Get starting point... word_tier_num j+1
						max_after = Get end point... word_tier_num j+1

						if length(item$)>1 and end_phoneme_plusone > min_after
							across_boundary$ = Get label of interval... word_tier_num j+1
						endif
						if length(item$)>2 and end_phoneme_plustwo > min_after
							across_boundary$ = Get label of interval... word_tier_num j+1
						endif
					endif
				endif
			endfor

			
			# Measure the centre of gravity in the middle of the s or sh sound
			measure = begin + (dur /2)

			select Sound 'object_name$'

			# windowing
			beginS = measure - 0.0125
			endS = measure + 0.0125
			Extract part... 'beginS' 'endS' Hamming 1 no
			snd = selected ("Sound")
			# preemphasis filter (Nissen, 2003)
			Filter (pre-emphasis)... 750
			sndPre = selected ("Sound")
			# calculate spectrum
			To Spectrum... yes
			# extract spectral moments
			cog = Get centre of gravity... 2

			# round the centre of gravity number to three decimal points
			fixed_cog$ = fixed$(cog,3)

			# save all the information in the result textfile
			fileappend "cog_outcome.txt" 'object_name$', 'participant', 'item$', 'word$', 'across_boundary$', 
					...'style$', 'labeliminustwo$', 'labeliminusone$', 'labeli$', 'labeliplusone$', 
					...'labeliplustwo$', 'fixed_begin$', 'fixed_dur$', 'fixed_cog$', 'newline$'

			# remove the selections
			plus snd
			plus sndPre
			Remove
		endif
	endfor

	# Clean up
	select Sound 'object_name$'
	plus TextGrid 'object_name$'
	Remove

endfor
select Strings filelist
Remove

